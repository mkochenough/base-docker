buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.7'
    }
}

repositories {
    jcenter()
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPullImage

apply plugin: 'com.bmuschko.docker-remote-api'

docker {
    if (System.env.DOCKER_HOST) {
        url = "$System.env.DOCKER_HOST".replace("tcp","http")
    }
    else {
        url = 'unix:///var/run/docker.sock'
    }
}

task updateUpstreamDebianImage(type: DockerPullImage) {
    repository = 'debian'
    tag = 'stretch'
}

task buildImage(type: DockerBuildImage) {
    dependsOn updateUpstreamDebianImage
    inputDir = file('default')
    tag = 'mkoch/base'
    noCache = true
}

task buildBackportsImage(type: DockerBuildImage) {
    dependsOn buildImage
    inputDir = file('backports')
    tag = 'mkoch/base-backports'
    noCache = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

